"""cxx_cxx_library macro"""

def cxx_cxx_library(
        name,
        crate_name,
        bridge_srcs,
        srcs = None,
        headers = None,
        deps = None,
        visibility = None):
    """Generate C++ source for one or more Rust cxx::bridges and bundle into a cc_library.

    Args:
        name: A unique name for the cc_library rule.
        crate_name: The name of the corresponding Rust crate, used as the include_prefix.
        bridge_srcs: One or more Rust source files containing cxx::bridge modules.
        srcs: Forwarded to cc_library unmodified.
        headers: Forwarded to cc_library unmodified.
        deps: Forwarded to cc_library unmodified
        visibility: Visibility for all rules generated by this macro.
    """

    gen_srcs = []
    gen_hdrs = {}
    for bridge_source in bridge_srcs:
        source_basename = bridge_source.rsplit("/", 1)[-1]
        header_target = source_basename + "-gen-header"
        source_target = source_basename + "-gen-source"

        hdr_out = source_basename + ".h"
        native.genrule(
            name = header_target,
            srcs = [bridge_source],
            out = hdr_out,
            cmd = "$(exe //:codegen) --header ${SRCS} > ${OUT}",
            visibility = visibility,
        )

        native.genrule(
            name = source_target,
            srcs = [bridge_source],
            out = source_basename + ".cc",
            cmd = "$(exe //:codegen) ${SRCS} > ${OUT}",
            visibility = visibility,
        )

        gen_hdrs[hdr_out] = ":" + header_target
        gen_srcs.append(":" + source_target)

    cxx_library(
        name = name,
        srcs = srcs + gen_srcs,
        headers = flatten_dicts(headers, gen_hdrs),
        header_namespace = crate_name,
        deps = deps,
        visibility = visibility,
    )

